group 'com.benbarkay.htmltopdf'
version '0.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'htmltopdf'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'An HTML to PDF conversion library written in Java, based on wkhtmltopdf.'
                url 'http://www.example.com/example-application'

                scm {
                    connection 'scm:git:git://github.com/benbarkay/htmltopdf-java.git'
                    developerConnection 'scm:git:ssh://github.com:benbarkay/htmltopdf-java.git'
                    url 'http://github.com/benbarkay/htmltopdf-java/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'benbarkay'
                        name 'Ben Barkay'
                        email 'benbarkay+htmltopdf@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile 'net.java.dev.jna:jna:[4.0.0,)'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
